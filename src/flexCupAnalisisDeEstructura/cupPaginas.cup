/*--------------- 1ra Area: Codigo de Usuario -----------------------*/
//-------> importaciones, paquetes

package recuperacionPaginas;
import java_cup.runtime.*;
import elementos.*;
import Objetos.*;
import java.util.ArrayList;
//import javax.swing.tree.DefaultMutableTreeNode;
//import backend.manejoDeDatos.*;

parser code {:


    int fila,columna;
    String lexema;
    //  VARIABLES
    ArrayList<Token> listaDeTokens;
    ArrayList<String> listaDeEtiquetas;
    ArrayList<Token> listaDeAtributos;
    // Connect this parser to a scanner!
    public parser(AnalizadorLexicoPaginas lex) {
	super(lex);
        listaDeTokens=new ArrayList<>();
        listaDeEtiquetas= new ArrayList<>();
        listaDeAtributos = new ArrayList<>();
    }

    //Metodo al que se llama automaticamente ante algun error sintactico
    public void syntax_error(Symbol s){        
        try {
        this.lexema =s.value.toString();
        String lexema = s.value.toString();
        int fila = s.left;
        int columna = s.right;
        System.out.println("ERROR"+lexema+" "+fila+" "+columna);
        } catch (Exception e) {
            System.out.println("Error Recuperable se esperaba mas simbolos en ultima fila");
        } 
    }

    //Metodo al que se llama en el momento en que ya no es posible una recuperacion de errores
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{        
        try {
            String lexema = s.value.toString();
            int fila = s.left;
            int columna = s.right;
            //this.clienteFrame.mostrarErrorSintactico("NO HAS CONTROLADO ESTE ERROR");
            //this.clienteFrame.setSeDebeEnviarElTexto(false);
        System.out.println("ERROR"+lexema+" "+fila+" "+columna);

        } catch (Exception e) {
            System.out.println("ERROR EN LA ESTRUCUTURA PRINCIPAL acciones");
        }
    }
/*
    private void iniciarSitioWeb(Object i,String tipo,int ileft,int iright){
  
    }


    private void iniciarPaginaWeb(Object i,String tipo,int ileft,int iright){

    }
*/
:}

/*--------------- 2da Area: Declaraciones -----------------------*/

terminal String         ELEMENTO,NOMBRE,PARAMETROS,PARAMETRO,ETIQUETA,VALOR,SITIO_WEB,
                        ID,PAGINA_WEB,TITULO,TITULO_PARAMETRO,PARRAFO,IMAGEN,VIDEO,MENU,SITIO,PADRE,
                        ETIQUETAS_DE_PAGINA,COMPONENTE,PAGINA,CLASE,TEXTO,ALINEACION,
                        ORIGEN,ALTURA,ANCHO,ETIQUETAS,CENTRAR,IZQUIERDA,DERECHA,
                        JUSTIFICAR,COLOR,DIAGONAL,MENOR_QUE,MAYOR_QUE,IDENTIFICADOR,
                        NUMERO,CONJUNTO_DE_ETIQUETAS,URL,ORACION,
                        USUARIO_CREACION,FECHA_CREACION,FECHA_MODIFICACION,USUARIO_MODIFICACION,
                        FORMATO_FECHA,IGUAL,COMILLA,ATRIBUTOS,ATRIBUTO,PARAM_ETIQUETAS;

non terminal Object     conjuntoDeElementos,elemento,inicioSitioWeb,inicioPaginaWeb,
                        inicioEtiquetas,inicioComponente,parametros,atributos,
                        fin,conjuntoDeParametros,unParametro,tipoDeClase,etiquetas,
                        conjuntoDeEtiquetas,unaEtiqueta,conjuntoDeAtributos,unAtributo,
                        tipoDeAlineacion,etiquetasMenu;
                        
       
/*--------------- 3ra Area: Reglas Semanticas -----------------------*/


start with conjuntoDeElementos;

conjuntoDeElementos::=conjuntoDeElementos elemento | elemento;


elemento::=  inicioSitioWeb parametros fin
             {:
                SitioWeb nuevoSitio=new SitioWeb();
                nuevoSitio.darValoresCreacion(listaDeTokens);
                Run.listaDeSitiosWeb.add(nuevoSitio);
                listaDeTokens=new ArrayList<>();
             :}|
             inicioPaginaWeb parametros fin
             {:
                PaginaWeb nuevaPagina=new PaginaWeb();
                nuevaPagina.darValoresCreacion(listaDeTokens);
                Run.listaDePaginasWeb.add(nuevaPagina);
                listaDeTokens=new ArrayList<>();
            :}|
             inicioEtiquetas parametros etiquetas fin
             {:
                String id =listaDeTokens.get(0).getLexema();
                Etiqueta nuevaEtiqueta = new Etiqueta(id.substring(1, id.length() - 1),listaDeEtiquetas);//lista de tokens posee el id
                Run.listaDeEtiquetas.add(nuevaEtiqueta);
                listaDeEtiquetas = new ArrayList<>();
                listaDeTokens=new ArrayList<>();
            :}|
             inicioComponente parametros atributos fin
             {:
                Componente nuevoComponente=new Componente();
                nuevoComponente.darValoresCreacion(listaDeTokens);//Parametros
                nuevoComponente.darValoresCreacion(listaDeAtributos);//Atributos segun clase
                Run.listaDeComponentes.add(nuevoComponente);
                listaDeTokens=new ArrayList<>();
                listaDeAtributos=new ArrayList<>();
             :};

fin::= MENOR_QUE DIAGONAL ELEMENTO MAYOR_QUE;

//***************************************************Inicio*************************************

inicioSitioWeb::=MENOR_QUE ELEMENTO NOMBRE IGUAL COMILLA SITIO_WEB COMILLA MAYOR_QUE{:System.out.println("SITIO WEB");:};

inicioPaginaWeb::=MENOR_QUE ELEMENTO NOMBRE IGUAL COMILLA PAGINA_WEB COMILLA MAYOR_QUE;

inicioEtiquetas::=MENOR_QUE ELEMENTO NOMBRE IGUAL COMILLA ETIQUETAS_DE_PAGINA COMILLA MAYOR_QUE;

inicioComponente::=MENOR_QUE ELEMENTO NOMBRE IGUAL COMILLA COMPONENTE COMILLA MAYOR_QUE;




//**************************************************Parametros*******************************************

parametros::=MENOR_QUE PARAMETROS MAYOR_QUE conjuntoDeParametros MENOR_QUE DIAGONAL PARAMETROS MAYOR_QUE;

conjuntoDeParametros::= conjuntoDeParametros unParametro | unParametro;

unParametro::= MENOR_QUE PARAMETRO NOMBRE IGUAL  COMILLA ID:i  COMILLA MAYOR_QUE  IDENTIFICADOR:t  MENOR_QUE DIAGONAL PARAMETRO MAYOR_QUE
               {:listaDeTokens.add(new Token(i,t));:}|
               
               MENOR_QUE PARAMETRO NOMBRE IGUAL  COMILLA USUARIO_CREACION:i  COMILLA MAYOR_QUE  IDENTIFICADOR:t  MENOR_QUE DIAGONAL PARAMETRO MAYOR_QUE
               {:listaDeTokens.add(new Token(i,t));:}|
               
               MENOR_QUE PARAMETRO NOMBRE IGUAL  COMILLA FECHA_CREACION:i COMILLA  MAYOR_QUE  FORMATO_FECHA:t  MENOR_QUE DIAGONAL PARAMETRO MAYOR_QUE
               {:listaDeTokens.add(new Token(i,t));:}|
               
                MENOR_QUE PARAMETRO NOMBRE IGUAL  COMILLA FECHA_MODIFICACION:i  COMILLA MAYOR_QUE FORMATO_FECHA:t  MENOR_QUE DIAGONAL PARAMETRO MAYOR_QUE
               {:listaDeTokens.add(new Token(i,t));:}|
               
               MENOR_QUE PARAMETRO NOMBRE IGUAL  COMILLA USUARIO_MODIFICACION:i COMILLA  MAYOR_QUE IDENTIFICADOR:t  MENOR_QUE DIAGONAL PARAMETRO MAYOR_QUE
               {:listaDeTokens.add(new Token(i,t));:}|
               
               MENOR_QUE PARAMETRO NOMBRE IGUAL  COMILLA TITULO_PARAMETRO:i  COMILLA MAYOR_QUE ORACION:t MENOR_QUE DIAGONAL PARAMETRO MAYOR_QUE
               {:listaDeTokens.add(new Token(i,t));:}|
               
               MENOR_QUE PARAMETRO NOMBRE IGUAL  COMILLA SITIO:i  COMILLA MAYOR_QUE  IDENTIFICADOR:t  MENOR_QUE DIAGONAL PARAMETRO MAYOR_QUE
               {:listaDeTokens.add(new Token(i,t));:}|
               
               MENOR_QUE PARAMETRO NOMBRE IGUAL  COMILLA PADRE:i  COMILLA MAYOR_QUE  IDENTIFICADOR:t  MENOR_QUE DIAGONAL PARAMETRO MAYOR_QUE
               {:listaDeTokens.add(new Token(i,t));:}|
               
               MENOR_QUE PARAMETRO NOMBRE IGUAL  COMILLA PAGINA:i  COMILLA MAYOR_QUE  IDENTIFICADOR:t  MENOR_QUE DIAGONAL PARAMETRO MAYOR_QUE
               {:listaDeTokens.add(new Token(i,t));:}|
              
               MENOR_QUE PARAMETRO NOMBRE IGUAL  COMILLA CLASE:i  COMILLA MAYOR_QUE tipoDeClase:t MENOR_QUE DIAGONAL PARAMETRO MAYOR_QUE
               {:listaDeTokens.add(new Token(i,t.toString()));:}|
               error;
               

tipoDeClase::=TITULO:e {:RESULT=e;:}|
              PARRAFO:e {:RESULT=e;:}|
              IMAGEN:e {:RESULT=e;:}|
              VIDEO:e {:RESULT=e;:}|
              MENU:e {:RESULT=e;:};





//****************************************************Etiquetas*****************************************

etiquetas::= MENOR_QUE PARAM_ETIQUETAS MAYOR_QUE conjuntoDeEtiquetas MENOR_QUE DIAGONAL PARAM_ETIQUETAS MAYOR_QUE{::};

conjuntoDeEtiquetas::= conjuntoDeEtiquetas unaEtiqueta|unaEtiqueta;

unaEtiqueta ::= MENOR_QUE ETIQUETA:i VALOR IGUAL  IDENTIFICADOR:t  DIAGONAL MAYOR_QUE
               {:listaDeEtiquetas.add(t.substring(1,t.length() - 1));:};






//****************************************************Atributos*********************************************

atributos::= MENOR_QUE ATRIBUTOS MAYOR_QUE conjuntoDeAtributos MENOR_QUE DIAGONAL ATRIBUTOS MAYOR_QUE;

conjuntoDeAtributos::= conjuntoDeAtributos unAtributo | unAtributo;

unAtributo::= MENOR_QUE ATRIBUTO  NOMBRE IGUAL  COMILLA TEXTO:i COMILLA  MAYOR_QUE ORACION:t MENOR_QUE DIAGONAL ATRIBUTO MAYOR_QUE
              {:listaDeAtributos.add(new Token(i,t));:}|
              
              MENOR_QUE ATRIBUTO  NOMBRE IGUAL  COMILLA ALINEACION:i  COMILLA MAYOR_QUE tipoDeAlineacion:t MENOR_QUE DIAGONAL ATRIBUTO MAYOR_QUE
              {:listaDeAtributos.add(new Token(i,t.toString()));:}|
              
              MENOR_QUE ATRIBUTO  NOMBRE IGUAL  COMILLA COLOR:i COMILLA  MAYOR_QUE ORACION:t MENOR_QUE DIAGONAL ATRIBUTO MAYOR_QUE
              {:listaDeAtributos.add(new Token(i,t));:}|

              MENOR_QUE ATRIBUTO  NOMBRE IGUAL  COMILLA ORIGEN:i  COMILLA MAYOR_QUE URL:t MENOR_QUE DIAGONAL ATRIBUTO MAYOR_QUE
              {:listaDeAtributos.add(new Token(i,t));:}|

              MENOR_QUE ATRIBUTO  NOMBRE IGUAL  COMILLA ALTURA:i  COMILLA MAYOR_QUE NUMERO:t MENOR_QUE DIAGONAL ATRIBUTO MAYOR_QUE
              {:listaDeAtributos.add(new Token(i,t));:}|

              MENOR_QUE ATRIBUTO  NOMBRE IGUAL  COMILLA ANCHO:i  COMILLA MAYOR_QUE NUMERO:t MENOR_QUE DIAGONAL ATRIBUTO MAYOR_QUE
              {:listaDeAtributos.add(new Token(i,t));:}|

              MENOR_QUE ATRIBUTO  NOMBRE IGUAL  COMILLA PADRE:i  COMILLA MAYOR_QUE IDENTIFICADOR:t MENOR_QUE DIAGONAL ATRIBUTO MAYOR_QUE
              {:listaDeAtributos.add(new Token(i,t));:}|

              MENOR_QUE ATRIBUTO  NOMBRE IGUAL  COMILLA ETIQUETAS:i  COMILLA MAYOR_QUE etiquetasMenu:t MENOR_QUE DIAGONAL ATRIBUTO MAYOR_QUE
              {:listaDeAtributos.add(new Token(i,t.toString()));:}|

              error;


tipoDeAlineacion::=CENTRAR:t {:RESULT =t;:}|
                   IZQUIERDA:t {:RESULT =t;:}|
                   DERECHA:t {:RESULT =t;:}|
                   JUSTIFICAR:t {:RESULT=t;:};

etiquetasMenu::= IDENTIFICADOR:e {:RESULT=e;:}|CONJUNTO_DE_ETIQUETAS:e {:RESULT=e;:};
            
